'---- PLAYER COLLISIONS ----


'-- VERIFY PLAYER COLLISION WITH BLOCKS --

SUB PLAYER_COLLISION

 LOCAL XTA,XTB,YTA,YTB

 'CHECK COLLISION ONLY WHEN WALKING
 IF X_SPD<>0 OR X_SPDX<>0 THEN

  'STRAFING?
  IF STRAFE=1 THEN T=COSANG: COSANG=SINANG: SINANG=T
  STRAFE=0

  X_ANT=X_POS:Y_ANT=Y_POS
  X_POS = X_POS + COSANG * X_SPD + COSANG * X_SPDX
  Y_POS = Y_POS + SINANG * Y_SPD + SINANG * Y_SPDY

  CHG=-1
  FOR I=0 TO 1 'LOOP FOR MAP TYPE (WALLS OR OBJECTS)

   IM=I

   FOR TT=0 TO 1 'LOOP FOR DISTANCE TEST

    IF TT=0 THEN
     'ALMOST ONE BLOCK AHEAD
     IF CHG=-1 THEN T=MAPW(INT(X_POS+COSANG * (X_SPD*2/R)),INT(Y_POS+SINANG * (Y_SPD*2/R)),I)
    ELSE
     'NEAR
     IF CHG=-1 THEN T=MAPW(INT(X_POS+COSANG * X_SPD/R),INT(Y_POS+SINANG * Y_SPD/R),I)
    ENDIF    

    'SCAN MAP TYPE
    IF I=0 THEN
     'WALLS
     IF T<>0 AND T<500 THEN X_POS=X_ANT:Y_POS=Y_ANT
    ELSE
     'OBJECTS
     SELECT CASE T
      CASE 10,11,64,31 TO 39:
       EXIT FOR  'CHANDELIER / CELL LIGHT / BLOOD / DEAD CORPSES AND VINES
      CASE 12: 
       PLAY_SFX("ADLIB_BONUS_CHEST"): CHG=TT
       SCORE=SCORE+1000: T_FLOOR=T_FLOOR+1             'CHEST - BONUS
       EXIT FOR
      CASE 13: 
       PLAY_SFX("ADLIB_BONUS_CHALICE"): CHG=TT
       SCORE=SCORE+500: T_FLOOR=T_FLOOR+1              'CHALICE - BONUS
       EXIT FOR
      CASE 14: 
       PLAY_SFX("ADLIB_BONUS_CROSS"): CHG=TT
       SCORE=SCORE+100: T_FLOOR=T_FLOOR+1              'CROSS - BONUS
       EXIT FOR
      CASE 15: 
       PLAY_SFX("ADLIB_BONUS_CROWN"): CHG=TT
       SCORE=SCORE+5000: T_FLOOR=T_FLOOR+1             'CROWN - BONUS
       EXIT FOR
      CASE 16:       
       PLAY_SFX("ADLIB_AMMO"): CHG=TT
       IF TT=0 THEN
        AMMO=AMMO+ABS(MAPW(INT(X_POS+COSANG * X_SPD*2/R),INT(Y_POS+SINANG * Y_SPD*2/R),2))
        MAPW(INT(X_POS+COSANG * X_SPD*2/R),INT(Y_POS+SINANG * Y_SPD*2/R),2)=0
       ELSE
        AMMO=AMMO+ABS(MAPW(INT(X_POS+COSANG * X_SPD/R),INT(Y_POS+SINANG * Y_SPD/R),2))
        MAPW(INT(X_POS+COSANG * X_SPD/R),INT(Y_POS+SINANG * Y_SPD/R),2)=0
       ENDIF
       EXIT FOR                                        'AMMO
      CASE 18: 
       PLAY_SFX("ADLIB_KEY"): CHG=TT
       K_BLUE=1: STATUS=1                              'BLUE KEY
       EXIT FOR
      CASE 19: 
       PLAY_SFX("ADLIB_AMMO"): CHG=TT: CL_WEAPON=2
       AMMO=AMMO+8:ON_WEAPON(2)=1                      'MACHINE GUN
       EXIT FOR
      CASE 20,23:
       IF HEALTH<100 THEN 
        PLAY_SFX("ADLIB_FOOD"): CHG=TT                 'FOOD / DOG FOOD
        IF T=20 THEN HEALTH=HEALTH+5 ELSE HEALTH=HEALTH+10
        IF HEALTH>100 THEN HEALTH=100
       ENDIF
       EXIT FOR
      CASE 21: 
       PLAY_SFX("ADLIB_KEY"): CHG=TT
       K_GOLD=1: STATUS=1                              'GOLD KEY
       EXIT FOR
      CASE 22: 
       PLAY_SFX("ADLIB_BIGWEAPON"): CHG=TT: CL_WEAPON=3
       AMMO=AMMO+16:ON_WEAPON(3)=1:HAPPY=30            'CHAIN GUN
       EXIT FOR
      CASE 25: 
       PLAY_SFX("ADLIB_1UP"): CHG=TT                   '1UP!
       HEALTH=100
       LIVES=LIVES+1: STATUS=1: S_FLOOR=S_FLOOR+1
       EXIT FOR
      CASE 26: 
       IF HEALTH<100 THEN                              'MEDKIT
        PLAY_SFX("ADLIB_MEDKIT"): CHG=TT
        HEALTH=HEALTH+25
        IF HEALTH>100 THEN HEALTH=100
       ENDIF
       EXIT FOR
      CASE 57: 
       PLAY_SFX("BJ_YEAH"): CHG=TT: PAUSE(1000)
       STATUS=1:STAGE_CLEAR(LEVEL+1)                   'ENDGAME TRIGGER
       EXIT FOR
      CASE ELSE:
       IF T<>0 THEN X_POS=X_ANT:Y_POS=Y_ANT            'SOLID OBJECTS
     END SELECT
    ENDIF

   NEXT TT

   IF CHG>=0 THEN EXIT FOR

  NEXT I

  XTA=INT(X_POS+COSANG * (X_SPD*2/R))
  YTA=INT(Y_POS+SINANG * (Y_SPD*2/R))
  XTB=INT(X_POS+COSANG * (X_SPD/R))
  YTB=INT(Y_POS+SINANG * (Y_SPD/R))

  'IS ANY CHANGE ON MAP? - REMOVE ITEM FROM ARRAY MAP
  IF CHG>=0 THEN 
   STATUS=1
   IF CHG=0 THEN MAPW(XTA,YTA,IM)=0
   IF CHG=1 THEN MAPW(XTB,YTB,IM)=0
  ENDIF

 ENDIF 'X_SPD <> 0

END SUB
